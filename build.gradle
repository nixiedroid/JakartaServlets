import java.text.SimpleDateFormat

plugins {
    id 'application'
    //  id 'com.github.johnrengelman.shadow'
    id("io.github.goooler.shadow") version "8.1.7"
    id 'war'
}

group 'com.nixiedroid.jakarta'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


shadowJar{
    archiveClassifier.set('fat')
}

war {
    enabled=true
}
application {
    mainClass = "com.nixiedroid.jakarta.rest.AppLauncher"
    applicationDefaultJvmArgs  = ["-Dserver.tomcat.accesslog.enabled=true"]
}
ext {
    junitVersion = '5.10.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly('jakarta.enterprise:jakarta.enterprise.cdi-api:4.1.0')
    compileOnly('jakarta.ws.rs:jakarta.ws.rs-api:4.0.0')
    compileOnly('jakarta.servlet:jakarta.servlet-api:6.1.0')
    implementation('org.hibernate:hibernate-core:6.5.2.Final')
   // implementation('org.glassfish.jaxb:jaxb-runtime:4.0.3')
    implementation('org.hibernate.validator:hibernate-validator:8.0.1.Final')

    // https://mvnrepository.com/artifact/org.apache.tomcat.embed/tomcat-embed-jasper
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:10.1.24'

    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'
    implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0'

    // https://mvnrepository.com/artifact/org.apache.taglibs/taglibs-standard-impl
    runtimeOnly 'org.apache.taglibs:taglibs-standard-impl:1.2.5'

    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    runtimeOnly 'org.postgresql:postgresql:42.7.3'


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}
processResources{
    from(projectDir){
        include ("src/main/webapp/**")
    }
    eachFile { details ->
        if (details.path.contains('webapp') && !details.path.contains('WEB-INF')) {
            details.path = details.relativePath.segments[3..-1].join("/")
            details.path = "META-INF/resources/$details.path"
        }
        if (details.path.contains('WEB-INF')) {
            details.path = details.relativePath.segments[3..-1].join("/")
            details.path = "META-INF/resources/$details.path"
        }
    }
    exclude("META-INF/ECLIPSE_.SF")
    exclude("META-INF/ECLIPSE_.RSA")
    exclude("META-INF/LICENSE.md")
    exclude("META-INF/LICENSE")
    exclude("META-INF/NOTICE")
    exclude("META-INF/NOTICE.md")
    duplicatesStrategy = DuplicatesStrategy.WARN
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                //'Add-Opens': 'java.prefs/java.util.prefs',
                "Main-Class": application.mainClass,
                "Created-By"            : "Gradle ${gradle.gradleVersion}",
                "Build-Timestamp"       : new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Jdk"             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS"              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
//    from {
//        configurations.runtimeClasspath.collect {
//             it.isDirectory() ? it : zipTree(it)
//
//        }
//    }
}


//https://docs.gradle.org/4.9/userguide/working_with_files.html#sec:reproducible_archives
//Make gradle builds deterministic
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}